plugins {
    id 'org.springframework.boot' version '2.0.0.RELEASE'
    id "org.sonarqube" version "2.6.2"
    id 'idea'
    id "io.franzbecker.gradle-lombok" version "5.0.0"
    id 'net.ltgt.apt' version '0.21'
    id 'jacoco'
    id 'java'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

repositories {
    mavenCentral()
    maven { url "https://clojars.org/repo" }
    jcenter()
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'


sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'uk.gov.cslearning'
version = '1.1.0'

bootJar {
    archiveName = 'learner-record.jar'
    mainClassName = 'uk.gov.cslearning.record.Application'
}


configurations {
    ajc
    aspects
    compile {
        extendsFrom aspects
    }
}

def aspectj = { destDir, aspectPath, inpath, classpath ->
    ant.taskdef(resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties",
            classpath: configurations.ajc.asPath)

    ant.iajc(
            maxmem: "1024m", fork: "true", Xlint: "ignore",
            destDir: destDir,
            aspectPath: aspectPath,
            inpath: inpath,
            classpath: classpath,
            source: project.sourceCompatibility,
            target: project.targetCompatibility
    )
}

compileJava {
    doLast {
        aspectj project.sourceSets.main.output.classesDir.absolutePath,
                configurations.aspects.asPath,
                project.sourceSets.main.output.classesDir.absolutePath,
                project.sourceSets.main.runtimeClasspath.asPath
    }
}

compileTestJava {
    doLast {
        aspectj project.sourceSets.test.output.classesDir.absolutePath,
                configurations.aspects.asPath,
                project.sourceSets.test.output.classesDir.absolutePath,
                project.sourceSets.test.runtimeClasspath.asPath
    }
}

test {
    systemProperty 'spring.profiles.active', 'test'
    finalizedBy jacocoTestReport
}

dependencies {

    ajc 'org.aspectj:aspectjtools:1.9.1'
    aspects 'org.springframework:spring-aspects:5.0.5.RELEASE'

    compile 'com.sun.xml.bind:jaxb-impl:2.1.2'
    compile 'javax.activation:activation:1.1'
    compile 'org.aspectj:aspectjrt:1.9.1'
    compile 'org.aspectj:aspectjweaver:1.9.1'
    compile 'org.projectlombok:lombok:1.18.4'
    annotationProcessor 'org.projectlombok:lombok:1.18.4'

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-jersey'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile 'org.springframework.boot:spring-boot-starter-cache'
    compile 'org.springframework.boot:spring-boot-configuration-processor'
    compile 'org.springframework.data:spring-data-jpa:2.0.5.RELEASE'
    compile 'org.springframework.security.oauth:spring-security-oauth2:2.3.3.RELEASE'

    compile group: 'org.springframework.security', name: 'spring-security-jwt', version: '1.1.1.RELEASE'

    compile group: "com.github.java-json-tools", name: "json-patch", version: "1.13"
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'

    compile group: 'com.microsoft.azure', name: 'applicationinsights-web-auto', version: '2.5.0'
    compile group: 'com.microsoft.azure', name: 'applicationinsights-logging-logback', version: '2.5.0'
    compile group: 'com.microsoft.azure', name: 'applicationinsights-spring-boot-starter', version: '2.5.0'

    compile 'com.h2database:h2:1.4.199'
    compile 'org.hibernate:hibernate-core:5.2.16.Final'
    compile 'org.hibernate:hibernate-entitymanager:5.2.16.Final'
    compile 'com.mysql:mysql-connector-j:8.0.33'

    compile group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '4.12.0'
    compile group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc-template', version: '4.12.0'

    compile 'org.flywaydb:flyway-core:5.0.7'

    compile 'com.google.code.gson:gson:2.8.2'
    compile 'com.google.guava:guava:23.5-jre'
    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'org.codehaus.janino:janino:3.0.8'
    compile 'org.reflections:reflections:0.9.11'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.5'
    compile 'uk.gov.service.notify:notifications-java-client:3.9.2-RELEASE'

    compile 'com.github.ben-manes.caffeine:caffeine:2.6.2'

    compile 'org.apache.commons:commons-collections4:4.4'
    implementation group: 'jakarta.interceptor', name: 'jakarta.interceptor-api', version: '2.2.0'


    compile group: 'org.hashids', name: 'hashids', version: '1.0.3'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.security:spring-security-test'
    testCompile 'org.powermock:powermock-api-mockito2:1.7.4'
    testCompile 'org.powermock:powermock-module-junit4:1.7.4'

    testCompile 'org.mockito:mockito-core:2.8.47'

    testCompile("org.springframework.boot:spring-boot-starter-test")
}

compileJava.dependsOn(processResources)

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}
